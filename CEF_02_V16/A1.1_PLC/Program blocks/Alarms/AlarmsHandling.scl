FUNCTION_BLOCK "AlarmsHandling"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR 
      IEC_Timer_0_Instance_4 {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      IEC_Timer_0_Instance_5 {InstructionName := 'TP_TIME'; LibVersion := '1.0'} : TP_TIME;
      SetAlarmInstance : Array[0..100] of "SetAlarm_PLC";
      AlarmPLCset {InstructionName := 'Program_Alarm'; LibVersion := '1.0'} : Array[0..100] of Program_Alarm;
   END_VAR

   VAR_TEMP 
      i : Int;
   END_VAR


BEGIN
	REGION PLC alarms
	    #SetAlarmInstance[0].iAlarmCondition := NOT "safety_signals"."3L+" AND "iEmgStop" AND "iEmgStopDoors1" AND "iEmgStopDoors2" AND "iEmgStopOperatorII_HydraulicAgregate" AND "iR1EmgOK_NHL";
	    #SetAlarmInstance[1].iAlarmCondition := NOT "safety_signals"."4L+" AND "safety_signals"."3L+" AND "iSafetyScannerOK";
	    #SetAlarmInstance[2].iAlarmCondition := NOT "iEmgStop";
	    #SetAlarmInstance[3].iAlarmCondition := NOT "iEmgStopDoors1";
	    #SetAlarmInstance[4].iAlarmCondition := NOT "iEmgStopDoors2";
	    #SetAlarmInstance[5].iAlarmCondition := NOT "iEmgStopOperatorII_HydraulicAgregate" AND "iEmergencyStopHydraulics1";
	    #SetAlarmInstance[6].iAlarmCondition := NOT "iEmgStopOperatorII_HydraulicAgregate" AND "iEmergencyStopHydraulics2";
	    #SetAlarmInstance[7].iAlarmCondition := NOT "iServiceDoorClosed1";
	    #SetAlarmInstance[8].iAlarmCondition := NOT "iServiceDoorClosed2";
	    #SetAlarmInstance[9].iAlarmCondition := NOT "iEmgStopOperatorII_HydraulicAgregate" AND NOT "iEmergencyStopHydraulics1" AND NOT "iEmergencyStopHydraulics2" AND NOT "iEmergencyStopConveyor";
	    #SetAlarmInstance[10].iAlarmCondition := NOT "iR1EmgOK_NHL";
	    #SetAlarmInstance[11].iAlarmCondition := NOT "iSafetyScannerOK" AND "iSafetyScannerResetNotRequired";
	    #SetAlarmInstance[12].iAlarmCondition := NOT "iSafetyScannerOK" AND NOT "iSafetyScannerResetNotRequired";
	    #SetAlarmInstance[13].iAlarmCondition := "qServiceDoorUnlock1" AND "iServiceDoorClosed1";
	    #SetAlarmInstance[14].iAlarmCondition := "qServiceDoorUnlock2" AND "iServiceDoorClosed2";
	    #SetAlarmInstance[15].iAlarmCondition := "iR1stopMess" AND "RobotMain_DB".R1.StartAllowed AND "iR1periReady";
	    #SetAlarmInstance[15].iTime := t#8s;
	    #SetAlarmInstance[16].iAlarmCondition := ("iR1tubePresentFront" XOR "AutoPositions".R1_Gripper.PartPresent) AND NOT "StationParameters".GhostMode;
	    #SetAlarmInstance[16].iTime := t#5s;
	    #SetAlarmInstance[17].iAlarmCondition := ("iR1tubePresentRear" XOR "AutoPositions".R1_Gripper.PartPresent) AND NOT "StationParameters".GhostMode;
	    #SetAlarmInstance[17].iTime := t#5s;
	    #SetAlarmInstance[18].iAlarmCondition := ("i3partPresent" XOR "AutoPositions".St3_Fitting.PartPresent) AND NOT "StationParameters".GhostMode;
	    #SetAlarmInstance[18].iTime := t#5s;
	    #SetAlarmInstance[19].iAlarmCondition := NOT "iAirPressure" AND "safety_signals"."3L+";
	    #SetAlarmInstance[19].iTime := t#5s;
	    #SetAlarmInstance[20].iAlarmCondition := false;
	    #SetAlarmInstance[21].iAlarmCondition := "RobotMain_DB".R1.StartReq AND NOT "iR1ext";
	    #SetAlarmInstance[21].iTime := t#5s;
	    #SetAlarmInstance[22].iAlarmCondition := "RobotMain_DB".R1.StartAllowed AND NOT "iR1periReady";
	    #SetAlarmInstance[22].iTime := t#5s;
	    #SetAlarmInstance[23].iAlarmCondition := NOT "iR1progAct" AND "RobotMain_DB".R1.StartAllowed AND NOT "iR1stopMess" AND "qR1moveEnable" AND "iR1periReady";
	    #SetAlarmInstance[23].iTime := t#5s;
	    #SetAlarmInstance[24].iAlarmCondition := NOT "RobotMain_DB".R1.StartAllowed AND "RobotMain_DB".R1.StartReq AND "iR1ext" AND NOT "iR1periReady";
	    #SetAlarmInstance[24].iTime := t#5s;
	    #SetAlarmInstance[25].iAlarmCondition := "ModeSelection_DB"."R1 EXT off during auto".X;
	    #SetAlarmInstance[26].iAlarmCondition := NOT "qR1_ST3free" AND "sq_robot_"."Dropoff part to jig".X;
	    #SetAlarmInstance[26].iTime := t#5s;
	    #SetAlarmInstance[27].iAlarmCondition := FALSE;
	    #SetAlarmInstance[27].iTime := t#5s;
	    #SetAlarmInstance[28].iAlarmCondition := FALSE;
	    #SetAlarmInstance[28].iTime := t#5s;
	    #SetAlarmInstance[29].iAlarmCondition := "DevicesData".Digiforce1.ProgramChangeNOK;
	    #SetAlarmInstance[30].iAlarmCondition := "DevicesData".Digiforce2.ProgramChangeNOK;
	//    #SetAlarmInstance[31].iAlarmCondition := "DevicesData".Digiforce3.ProgramChangeNOK;
	//    #SetAlarmInstance[32].iAlarmCondition := "DevicesData".Digiforce4.ProgramChangeNOK;
	    #SetAlarmInstance[33].iAlarmCondition := "Devices_Diagnostic_DB".PNmissing;
	    #SetAlarmInstance[33].iTime := t#5s;
	    #SetAlarmInstance[34].iAlarmCondition := NOT "DevicesData".Digiforce1.Ready AND NOT "DevicesData".Digiforce1.Start;
	    #SetAlarmInstance[34].iTime := t#5s;
	    #SetAlarmInstance[35].iAlarmCondition := NOT "DevicesData".Digiforce2.Ready AND NOT "DevicesData".Digiforce2.Start;
	    #SetAlarmInstance[35].iTime := t#5s;
	//    #SetAlarmInstance[36].iAlarmCondition := NOT "DevicesData".Digiforce3.Ready AND NOT "DevicesData".Digiforce3.Start;
	    #SetAlarmInstance[36].iTime := t#5s;
	//    #SetAlarmInstance[37].iAlarmCondition := NOT "DevicesData".Digiforce4.Ready AND NOT "DevicesData".Digiforce4.Start;
	    #SetAlarmInstance[37].iTime := t#5s;
	    #SetAlarmInstance[38].iAlarmCondition := FALSE;
	    #SetAlarmInstance[38].iTime := t#5s;
	    #SetAlarmInstance[39].iAlarmCondition := "iHydraulicCircuitBreakerQ2off";
	    #SetAlarmInstance[39].iTime := t#5s;
	    #SetAlarmInstance[40].iAlarmCondition := "iOilTemperature50" AND NOT "iOilTemperature65";
	    #SetAlarmInstance[40].iTime := t#1s;
	    #SetAlarmInstance[41].iAlarmCondition := "iOilTemperature65";
	    #SetAlarmInstance[41].iTime := t#1s;
	    #SetAlarmInstance[42].iAlarmCondition := NOT "iOilLowLevelOK";
	    #SetAlarmInstance[42].iTime := t#1s;
	    #SetAlarmInstance[43].iAlarmCondition := "DevicesData".Friedrich1.Error ;
	    #SetAlarmInstance[44].iAlarmCondition := "DevicesData".Friedrich2.Error;
	    #SetAlarmInstance[45].iAlarmCondition := "DB_Devices_Instance".FriedrichUnit1_Instance.CommunicationError;
	    #SetAlarmInstance[46].iAlarmCondition := "DB_Devices_Instance".FriedrichUnit2_Instance.CommunicationError;
	    #SetAlarmInstance[47].iAlarmCondition := ("iAEtubePresentFront" XOR "AutoPositions".AE_Gripper.PartPresent) AND NOT "StationParameters".GhostMode;
	    #SetAlarmInstance[47].iTime := t#5s;
	    #SetAlarmInstance[48].iAlarmCondition := ("iAEtubePresentRear" XOR "AutoPositions".AE_Gripper.PartPresent) AND NOT "StationParameters".GhostMode;
	    #SetAlarmInstance[48].iTime := t#5s;
	    #SetAlarmInstance[49].iAlarmCondition := ("i4partPresent" XOR "AutoPositions".St4_OKposition.PartPresent) AND NOT "StationParameters".GhostMode;
	    #SetAlarmInstance[49].iTime := t#5s;
	    
	    #SetAlarmInstance[50].iAlarmCondition := NOT "iR1rearInWorkZone1" AND NOT "iR1rearInWorkZone2" AND NOT "iR1rearInWorkZone3";
	    #SetAlarmInstance[50].iTime := t#100ms;
	    #SetAlarmInstance[51].iAlarmCondition := NOT "iEmgStopOperatorII_HydraulicAgregate" AND "iEmergencyStopConveyor";
	    #SetAlarmInstance[52].iAlarmCondition := "Main_Safety_RTG1_DB"."FDBACK_A2.7".ERROR;
	    #SetAlarmInstance[53].iAlarmCondition := "Main_Safety_RTG1_DB"."FDBACK_A2.8".ERROR;
	    #SetAlarmInstance[54].iAlarmCondition := "Main_Safety_RTG1_DB"."FDBACK_A2.9".ERROR;
	    #SetAlarmInstance[55].iAlarmCondition := "Main_Safety_RTG1_DB".FDBACK_KF101x.ERROR;
	    #SetAlarmInstance[56].iAlarmCondition := "Main_Safety_RTG1_DB".FDBACK_KM20_KM21.ERROR;
	    #SetAlarmInstance[57].iAlarmCondition := (NOT "Manipulator_main_DB".GetCommOK OR NOT "Manipulator_main_DB".PutCommOK) AND NOT "StationParameters".ManipulatorCommOff;
	    #SetAlarmInstance[57].iTime := t#0s;
	    #SetAlarmInstance[58].iAlarmCondition := NOT "iOilHighLevekOK";
	    #SetAlarmInstance[59].iAlarmCondition := "ValueContinuityCheck_VH1_DB".error;
	    #SetAlarmInstance[60].iAlarmCondition := "ValueContinuityCheck_VH2_DB".error;
	    
	    
	    
	    FOR #i := 0 TO 100 DO
	        #SetAlarmInstance[#i](iReset := "Alarms".Reset,
	                              AlarmToSet => "Alarms".AlarmPLC[#i]);
	        #AlarmPLCset[#i](SIG := "Alarms".AlarmPLC[#i]);
	    END_FOR;
	END_REGION
	REGION warnings
	    "Alarms".Warning[1] := FALSE;
	    "Alarms".Warning[2] := "iOilFilter1";
	    "Alarms".Warning[3] := "iOilFilter2";
	    "Alarms".Warning[4] := "ModeSelection_DB".mode.switch.poloautomaActivated;
	    
	    
	END_REGION
	
	REGION check alarm active
	    "SystemHMI".AlarmActive := "RobotJob_DB".ERR_FLT OR
	    "sq_carousel_main_".ERR_FLT OR
	    "sq_carousel_insertion_st1".ERR_FLT OR
	    "sq_carousel_unloading_st2".ERR_FLT OR
	    "sq_fitting_station_st3".ERR_FLT OR
	    "sq_hanger_manipulator".ERR_FLT OR
	    "sq_robot_".ERR_FLT OR
	    "sq_autoeject_DB".ERR_FLT;
	    FOR #i := 1 TO 256 DO
	        IF "Alarms".Alarm[#i] THEN
	            "SystemHMI".AlarmActive := true;
	        END_IF;
	    END_FOR;
	    FOR #i := 0 TO 100 DO
	        IF "Alarms".AlarmPLC[#i] THEN
	            "SystemHMI".AlarmActive := true;
	        END_IF;
	    END_FOR;
	END_REGION
	REGION check warning active
	    "SystemHMI".WarningActive := false;
	    FOR #i := 1 TO 256 DO
	        IF "Alarms".Warning[#i] THEN
	            "SystemHMI".WarningActive := true;
	        END_IF;
	    END_FOR;
	END_REGION
	REGION Alarm active blinking
	    "SystemHMI".AlarmActiveBlinking := ("SystemHMI".AlarmActive AND "Clock_1Hz") OR ("Alarms".NOKInAuto AND "Clock_1Hz");
	    "Alarms".NOKInAuto := false;
	END_REGION
	REGION Reset alarms
	    "Alarms".Reset := "iBtnResetAlarm" OR "Alarms".ResetHmi;
	    IF "Alarms".Reset THEN
	        FOR #i := 1 TO 256 DO
	            "Alarms".Alarm[#i] := false;
	            "Alarms".Warning[#i] := false;
	        END_FOR;
	    END_IF;
	END_REGION
	REGION Reset part errors
	    IF "SystemHMI".ResetPartsErrors THEN
	        FOR #i := 1 TO 256 DO
	            "Alarms".PartErrorsHMI[#i] := false;
	        END_FOR;
	    END_IF;
	    "SystemHMI".ResetPartsErrors := false;
	END_REGION
	REGION reset main air valve
	    #IEC_Timer_0_Instance_4(IN := "Alarms".Reset AND "safety_signals"."3L+" AND NOT "iAirPressure",
	                            PT := t#3s);
	    #IEC_Timer_0_Instance_5(IN := #IEC_Timer_0_Instance_4.Q,
	                            PT := t#1s,
	                            Q => "qMS6reset");
	END_REGION
	
	
	
	
	
END_FUNCTION_BLOCK

