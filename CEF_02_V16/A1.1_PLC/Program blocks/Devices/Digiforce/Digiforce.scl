FUNCTION_BLOCK "Digiforce"
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      DataRead : HW_IO;
      DataWrite : HW_IO;
   END_VAR

   VAR_IN_OUT 
      status : "UDT_Digiforce";
   END_VAR

   VAR 
      send { S7_SetPoint := 'False'} : "UDT_Digiforce_send";
      receive : "UDT_Digiforce_receive";
      dataAtmp { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} AT receive : Array[0..22] of DWord;
      IEC_Timer_zmenaProgramu {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
   END_VAR

   VAR_TEMP 
      retval : Int;
      i : Int;
      programActual : USInt;
      programOK : Bool;
      ProgramChangeNOK : Bool;
   END_VAR


BEGIN
	//nacist data z digiforce
	#retval := DPRD_DAT(LADDR := #DataRead, RECORD => #receive);
	
	//otocit poradi byte v analogovych datech
	FOR #i := 1 TO 22 DO
	    #dataAtmp[#i] := SWAP_DWORD(IN := #dataAtmp[#i]);
	END_FOR;
	
	//nastaveni programu
	#send.Prog := #status.programSet;
	
	//kontrola aktualniho programu
	#programActual := BOOL_TO_USINT(#receive.dataD.Prog0) +
	BOOL_TO_USINT(#receive.dataD.Prog1) * 2 +
	BOOL_TO_USINT(#receive.dataD.Prog2) * 4+
	BOOL_TO_USINT(#receive.dataD.Strobe) * 8;
	#programOK := #programActual = #status.programSet;
	
	//odeslani pozadavku na zmenu programu
	#send.Strobe := "Clock_0.5Hz" AND NOT #programOK;
	
	//chyba zmeny programu
	#IEC_Timer_zmenaProgramu(IN := NOT #programOK,
	                         PT := t#10s,
	                         Q => #ProgramChangeNOK);
	
	#status.programActual := #programActual;
	#status.programOK := #programOK;
	#status.ProgramChangeNOK := #ProgramChangeNOK;
	#status.ActualWay := #receive.dataA[21];
	#status.ActualForce := #receive.dataA[22];
	#status.Ready := #receive.dataD.Ready;
	#status.ResultOK := #receive.dataD.OK;
	#status.ResultNOK := #receive.dataD.NOK;
	
	//Auto mode
	#send.Auto := #status.Auto;
	//Start measuring
	#send.Start := #status.Start;
	//Tare
	#send.Tare_X := #status.TareX;
	#status.TareX := false;
	
	
	
	
	
	//odeslat data do digiforce
	#retval := DPWR_DAT(LADDR := #DataWrite, RECORD := #send);
END_FUNCTION_BLOCK

