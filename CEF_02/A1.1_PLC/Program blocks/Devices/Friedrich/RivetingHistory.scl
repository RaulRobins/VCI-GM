FUNCTION_BLOCK "RivetingHistory"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      iNewCycle : Bool;
      iData : "UDT_Friedrich";
   END_VAR

   VAR RETAIN
      history { S7_SetPoint := 'False'} : Array[0..99] of "UDT_Friedrich";
   END_VAR
   VAR 
      pageData : Array[0..9] of "UDT_Friedrich";
   END_VAR
   VAR RETAIN
      pageNumber { S7_SetPoint := 'True'} : USInt;
   END_VAR
   VAR 
      resetHistory : Bool;
   END_VAR

   VAR_TEMP 
      i : USInt;
      retval : Int;
   END_VAR


BEGIN
	IF #iNewCycle THEN
	    #retval := MOVE_BLK_VARIANT(SRC := #history, COUNT := 99, SRC_INDEX := 0, DEST_INDEX := 1, DEST => #history);
	    #history[0] := #iData;
	END_IF;
	
	CASE #pageNumber OF
	    0..9:
	        #retval := MOVE_BLK_VARIANT(SRC := #history, COUNT := 10, SRC_INDEX := #pageNumber * 10, DEST_INDEX := 0, DEST => #pageData);
	    ELSE
	        #pageNumber := 0;
	END_CASE;
	
	IF #resetHistory THEN
	    #resetHistory := FALSE;
	    #history[0].LastH1 := 0.0;
	    #history[0].LastH2 := 0.0;
	    #history[0].LastTime := 0.0;
	    #history[0].LastZ1 := 0.0;
	    #history[0].LastZ2 := 0.0;
	    #history[0].H1OK := FALSE;
	    #history[0].H2OK := FALSE;
	    #history[0].NOK := FALSE;
	    #history[0].OK := FALSE;
	    #history[0].TimeOK := FALSE;
	    #history[0].Z1OK := FALSE;
	    #history[0].PrgNumber := 0;
	    FOR #i := 1 TO 99 DO
	        #history[#i] := #history[0];
	    END_FOR;
	END_IF;
	
	
	
	
END_FUNCTION_BLOCK

